<program> ::= <definitions>

<definitions> ::=
	<definitions> <definition>
    | <definition>
   
<definition> ::=
	<comment>
	| <var>
    | <def>
    | <data>

<comment> ::=
	<SINGLE_COMMENT>
    | <MULTILINE_COMMENT>
    

<def> ::= 
	<DEF> <LID> <lowercaseParams> <EQUAL> <O_CURLY> <opperations> <C_CURLY>

<opperations> ::=
	<comp>
    | <aAdd>

<lowercaseParams> ::=
	E
    | <lowercaseParams> <LID>

<aAdd> ::=
	<aAdd> <PLUS> <aMul>
    | <aAdd> <MIN> <aMul>
    | <aMul>

<aMul> ::=
	<aMul> <TIMES> <app>
    | <aMul> <DIVIDE> <app>
    | <index_at>

<index_at> ::=
	<LID> <AT> <aAdd>
    | <list> <AT> <aAdd>
    | <app>

<app> ::=
	<INT> | <STR> | <list>
    | <LID> | <UID>
    | <O_PAREN> <aAdd> <C_PAREN>
    | <map>

<map> ::= <MAP> <aAdd> <TO> <O_CURLY> <branches> <C_CURLY>

<branches> ::=
	<branches> <branch>
    | <branch>
    
<branch> ::=
	<pattern> <ARROW> <O_CURLY> <aAdd> <C_CURLY>
    
<pattern> ::=
	<LID>
    | <UID> <lowercaseParams>
    
<data> ::= <DATA> <UID> <EQUAL> <O_CURLY> <constructors> <C_CURLY>

<constructors> ::=
	<constructors> <COMMA> <constructor>
    | <constructor>

<constructor> ::=
	<UID> <uppercaseParams>
    
<uppercaseParams> ::=
	E
    | <uppercaseParams> <UID>
    
<var> ::= <VAR> <LID> <EQUAL> <opperations>

<list> ::= <O_SQUARE> <elements> <C_SQUARE>

<elements> ::=
	E
	| <int_elements>
    | <str_elements>

<int_elements> ::=
	<int_elements> <COMMA> <INT>
    | <INT>
    
<str_elements> ::=
	<str_elements> <COMMA> <STR>
    | <STR>

<comp> ::=
	<apply> <LID> <COMP> <LID>
    | <apply>
    
<apply> ::=
	E
    | <apply> <LID> <COMP>

<SINGLE_COMMENT> ::= "\/\/[^\n]*"
<MULTILINE_COMMENT> ::= "/\*[^*]*\*+(?:[^/*][^*]*\*+)*/"
<VAR> ::= "var"
<DATA> ::= "data"
<DEF> ::= "def"
<MAP> ::= "map"
<TO> ::= "to"
<EQUAL> ::= "="
<COMMA> ::= ","
<ARROW> ::= "\->"
<AT> ::= "@"
<COMP> ::= "\."
<O_CURLY> ::= "\{"
<C_CURLY> ::= "\}"
<O_PAREN> ::= "\("
<C_PAREN> ::= "\)"
<O_SQUARE> ::= "\["
<C_SQUARE> ::= "\]"
<PLUS> ::= "\+"
<MIN> ::= "\-"
<TIMES> ::= "\*"
<DIVIDE> ::= "\/"
<INT> ::= "[0-9]+"
<STR> ::= "\"[^\"]*\""
<LID> ::= [a-z] ([a-z] | [A-Z] | "_")*
<UID> ::= [A-Z] ([a-z] | [A-Z] | "_")*

